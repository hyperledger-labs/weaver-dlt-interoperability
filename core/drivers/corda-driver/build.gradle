buildscript {
    ext.kotlin_version = "1.3.61"
    ext.coroutines_version = "1.3.3"
    ext.protobuf_version = "3.11.1"
    ext.grpc_version = "1.28.1" // CURRENT_GRPC_VERSION
    ext.grpc_kotlin_version = "0.1.3" // CURRENT_GRPC_KOTLIN_VERSION
    ext.corda_release_group = "net.corda"
    ext.corda_core_release_group = "net.corda"
    ext.corda_version = "4.4"
    ext.corda_core_version = "4.4"
    ext.arrow_version = "0.10.4"

    repositories {
        mavenCentral()
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
    }
   dependencies {
       classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
   }
}

plugins {
    id "idea"
    id "application"
    id "com.google.protobuf" version "0.8.12"
    id 'org.jetbrains.dokka' version '0.10.1'
}

Properties constants = new Properties()

repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url 'https://software.r3.com/artifactory/corda' }
    flatDir {
        dirs '../../network/corda-interop-app/interop-contracts/build/libs'
        dirs '../../network/corda-interop-app/interop-workflows/build/libs'
    }
}

apply plugin: "kotlin"

dependencies {
    // This repo
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    compile "io.arrow-kt:arrow-core:$arrow_version"

    // Java
    compileOnly "javax.annotation:javax.annotation-api:1.2"

    // Google
    implementation "com.google.guava:guava:28.2-jre"
    compile 'com.google.code.gson:gson:2.8.6'

    // Corda dependencies.
    implementation "net.corda:corda-core:4.4"
    implementation "net.corda:corda-node-api:4.4"
    implementation "net.corda:corda:4.4"
    implementation "net.corda:corda-rpc:4.4"

    implementation(group: 'com.cordaInteropApp', name: 'interop-contracts', version: "0.6")
    implementation(group: 'com.cordaInteropApp', name: 'interop-workflows', version: "0.6")

    // gRPC and protobuf dependencies
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"
    implementation "com.google.protobuf:protobuf-java:$protobuf_version"
    implementation "com.google.protobuf:protobuf-java-util:$protobuf_version"
    implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"

    testImplementation("org.junit.jupiter:junit-jupiter:5.6.2")
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        languageVersion = "1.3"
        apiVersion = "1.3"
        jvmTarget = "1.8"
        javaParameters = true
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobuf_version" }
    plugins {
        // Specify protoc to generate using kotlin protobuf plugin
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
        // Specify protoc to generate using our grpc kotlin plugin
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpc_kotlin_version"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                // Generate Java gRPC classes
                grpc { }
                // Generate Kotlin gRPC using the custom plugin from library
                grpckt { }
            }
        }
    }
}

mainClassName = "com.cordaDriver.CordaDriverKt"

tasks.test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}
