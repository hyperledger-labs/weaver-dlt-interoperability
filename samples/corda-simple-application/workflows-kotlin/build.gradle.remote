apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.quasar-utils'

Properties constants = new Properties()
file("$projectDir/../artifactory.properties").withInputStream { constants.load(it) }

repositories {
    maven {
        url 'https://na.artifactory.swg-devops.com:443/artifactory/res-dlt-interop-maven-local/'
        credentials {
            username constants.username
            password constants.password
        }
    }
}

cordapp {
    targetPlatformVersion corda_platform_version
    minimumPlatformVersion corda_platform_version
    workflow {
        name "Template Flows"
        vendor "Corda Open Source"
        licence "Apache License, Version 2.0"
        versionId 1
    }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
    test {
        resources {
            srcDir rootProject.file("config/test")
        }
    }
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/kotlin')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "junit:junit:$junit_version"

    // Corda dependencies.
    cordaCompile "$corda_core_release_group:corda-core:$corda_core_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"

    testCompile "$corda_release_group:corda-node-driver:$corda_release_version"

    // CorDapp dependencies.
    cordapp project(":contracts-kotlin")
}

task integrationTest(type: Test, dependsOn: []) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

artifactory {
    contextUrl = 'https://na.artifactory.swg-devops.com:443/artifactory/'

    publish {
        contextUrl = 'https://na.artifactory.swg-devops.com:443/artifactory/'
        repository {
            repoKey = 'res-dlt-interop-maven-local'
            username = constants.username
            password = constants.password
            maven = true
        }
        defaults {
            publications ('mavenJava')
        }
    }
}

publishing {
    publications {
        workflows(MavenPublication) {
            groupId = 'com.cordaSimpleApplication'
            artifactId = 'workflows-kotlin'
            version = '0.4'
            from components.java
        }
    }
    repositories {
        maven {
            url 'https://na.artifactory.swg-devops.com:443/artifactory/res-dlt-interop-maven-local/'
            credentials {
                username constants.username
                password constants.password
            }
        }
    }
}